#version 330
layout(location=0) out vec4 fragColor;

in VertexData 
{
  vec4 position;
  vec4 normal;
  vec4 color;
  vec3 shadowPos1;
  vec3 shadowPos2;
  vec3 shadowPos3;
  vec2 uv;
} VertexOut;

uniform vec3 sunDirection;
uniform vec3 CameraPosition;
uniform sampler2D shadowTexture1;
uniform sampler2D shadowTexture2;
uniform sampler2D shadowTexture3;
uniform sampler2D  colormap;

float light()
{
  float diff = 0.6 * dot(sunDirection,VertexOut.normal.rgb);
  float ambi = 0.4;
  float shadow = 0;
  if ((VertexOut.shadowPos1.x<1.0) && 
      (VertexOut.shadowPos1.x>0) &&
      (VertexOut.shadowPos1.y<1.0) &&
      (VertexOut.shadowPos1.y>0))
  {
     if (VertexOut.shadowPos1.z-textureLod(shadowTexture1,VertexOut.shadowPos1.xy,0).z > 0.0001)
          shadow =1;
  }
  else
  {
    if ((VertexOut.shadowPos2.x<1.0) && (VertexOut.shadowPos2.x>0)&&(VertexOut.shadowPos2.y<1.0) && (VertexOut.shadowPos2.y>0))
    {
       if (VertexOut.shadowPos2.z-textureLod(shadowTexture2,VertexOut.shadowPos2.xy,0).z > 0.001)
            shadow =1;
    }
    else
    {
      if ((VertexOut.shadowPos3.x<1.0) && (VertexOut.shadowPos3.x>0)&&(VertexOut.shadowPos3.y<1.0) && (VertexOut.shadowPos3.y>0))
         if (VertexOut.shadowPos3.z-textureLod(shadowTexture3,VertexOut.shadowPos3.xy,0).z > 0.001)
              shadow =1;
    }
  }

  vec3 r = normalize(CameraPosition-VertexOut.position.xyz);
  float d = dot(sunDirection,VertexOut.normal.xyz);
  vec3 h = sunDirection - (d * VertexOut.normal.xyz);
  vec3 q = (d * VertexOut.normal.xyz) - h;
  float spec = pow(max(0,dot(q,r)),1000.);

  return ambi + max(0,diff)*(1-shadow);
}

vec3 makeFog(vec3 c)
{
  float dist = length(VertexOut.position.xyz-CameraPosition);
  float fogAmount = 1.0-exp(-dist * 0.0005);
  float sunAmount = max( dot( normalize(VertexOut.position.xyz-CameraPosition) , sunDirection ), 0.0 );
  vec3  fogColor  = mix( vec3(0.5,0.6,0.7), // bluish
                         vec3(1.0,0.9,0.7), // yellowish
                         pow(sunAmount,8.0) );
  return mix(c,fogColor,fogAmount);
}

void main()
{
  vec4 textureColor = textureLod(colormap,vec2(VertexOut.uv.x,1-VertexOut.uv.y),0);
  if (textureColor.a < 0.0001) discard;
  vec3 color = makeFog(light() * VertexOut.color.rgb * textureColor.rgb);
  fragColor = vec4(color ,textureColor.a);
}
